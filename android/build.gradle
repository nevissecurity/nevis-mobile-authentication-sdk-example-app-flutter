ext.getConfig = { String name ->
    File localPropertiesFile = project.rootProject.file('local.properties')
    if (localPropertiesFile?.exists()) {
        Properties localProperties = new Properties()
        localProperties.load(localPropertiesFile.newDataInputStream())
        if (localProperties.containsKey(name)) {
            return localProperties.getProperty(name)
        }
    }
    def env = System.getenv(name)
    if (env != null) {
        return env
    }
    def prop = System.getProperty(name)
    if (prop != null) {
        return prop
    }
    if (project.hasProperty(name)) {
        return project.getProperty(name)
    }

    throw new GradleException("Getting configuration with name $name failed! Set it as environment variable or as local/project/system property.")
}

allprojects {
    repositories {
        google ()
        mavenCentral ()
        maven {
            url "https://maven.pkg.github.com/nevissecurity/nevis-mobile-authentication-sdk-android-package"
            credentials {
                username = getConfig("GH_USERNAME")
                password = getConfig("GH_PERSONAL_ACCESS_TOKEN")
            }
        }
    }
}

rootProject.layout.buildDirectory = file('../build')
subprojects {
    def directory = "${rootProject.layout.buildDirectory.get().asFile}/${project.name}"
    project.layout.buildDirectory = file(directory)
}
subprojects {
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.layout.buildDirectory
}
