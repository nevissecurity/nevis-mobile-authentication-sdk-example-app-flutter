/*
 * Copyright Â© 2022 Nevis Security AG. All rights reserved.
 */

buildscript {
    ext.gradle_version = '8.3.0'
    ext.kotlin_version = '1.8.22'

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradle_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

ext.getConfig = { String name ->
    File localPropertiesFile = project.rootProject.file('local.properties')
    if (localPropertiesFile?.exists()) {
        Properties localProperties = new Properties()
        localProperties.load(localPropertiesFile.newDataInputStream())
        if (localProperties.containsKey(name)) {
            return localProperties.getProperty(name)
        }
    }
    def env = System.getenv(name)
    if (env != null) {
        return env
    }
    def prop = System.getProperty(name)
    if (prop != null) {
        return prop
    }
    if (project.hasProperty(name)) {
        return project.getProperty(name)
    }

    println("Getting env variable failed, returning empty: set $name as environment variable or as system property in your ~/.gradle/gradle.properties")
    return ""
}

allprojects {
    repositories {
        google ()
        mavenCentral ()
        maven {
            url "https://maven.pkg.github.com/nevissecurity/nevis-mobile-authentication-sdk-android-package"
            credentials {
                username = getConfig("GITHUB_USERNAME")
                password = getConfig("GITHUB_PERSONAL_ACCESS_TOKEN")
            }
        }
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
